apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
def minutesSinceEpoch = (new Date()).getTime()/(1000 * 60)
Properties keyRing = new Properties()
keyRing.load(new FileInputStream(rootProject.file('android.giftv.keyring')))
android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.icenerd.giftv"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 20
        versionName "2.0"
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        resValue "string", "network_service_type", "_giftv._tcp."
        resValue "string", "api_giphy", "http://api.giphy.com/v1/gifs"
        resValue "string", "network_service_secret", keyRing["network_service_secret"]
        resValue "string", "api_giphy_key", keyRing["api_giphy_key"]
    }
    signingConfigs {
        release {
            storeFile rootProject.file(keyRing["storeFile"])
            storePassword keyRing["storePassword"]
            keyAlias keyRing["keyAlias"]
            keyPassword keyRing["keyPassword"]
        }
    }
    buildTypes {
        debug {
            buildConfigField("String", "BUILD_TIME", "\"0\"")
            resValue("string", "build_time", "0")
        }
        release {
            signingConfig signingConfigs.release
            buildConfigField("String", "BUILD_TIME", "\"${minutesSinceEpoch}\"")
            resValue("string", "build_time", "${minutesSinceEpoch}")
        }
    }
}
dependencies {
    implementation 'com.android.support:leanback-v17:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
