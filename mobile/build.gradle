apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
def minutesSinceEpoch = (new Date()).getTime()/(1000 * 60) as long
Properties keyRing = new Properties()
keyRing.load(new FileInputStream(rootProject.file('android.giftv.keyring.example')))
android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.icenerd.giftv"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 21
        versionName "2.1"
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        resValue "string", "network_service_type", "_giftv._tcp."
        resValue "string", "api_giphy", "https://api.giphy.com/v1/gifs"
        resValue "string", "network_service_secret", keyRing["network_service_secret"]
        resValue "string", "api_giphy_key", keyRing["api_giphy_key"]
    }
    signingConfigs {
        release {
            storeFile rootProject.file(keyRing["storeFile"])
            storePassword keyRing["storePassword"]
            keyAlias keyRing["keyAlias"]
            keyPassword keyRing["keyPassword"]
        }
    }
    buildTypes {
        debug {
            buildConfigField("String", "BUILD_TIME", "\"0\"")
            resValue("string", "build_time", "0")
        }
        release {
            signingConfig signingConfigs.release
            buildConfigField("String", "BUILD_TIME", "\"${minutesSinceEpoch}\"")
            resValue("string", "build_time", "${minutesSinceEpoch}")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
dependencies {
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.16'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$vKotlin"
}
repositories {
    mavenCentral()
}
